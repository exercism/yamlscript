!yamlscript/v0

barn =::
- fly: I don't know why she swallowed the fly. Perhaps she'll die.
- spider: It wriggled and jiggled and tickled inside her.
- bird: How absurd to swallow a bird!
- cat: Imagine that, to swallow a cat!
- dog: What a hog, to swallow a dog!
- goat: Just opened her throat and swallowed a goat!
- cow: I don't know how she swallowed a cow!
- horse: She's dead, of course!

defn recite(start-verse end-verse):
  drop-last:
    reduce: add-verse [] (start-verse.-- .. end-verse.--)

defn add-verse(lines verse):
  animal =: barn.$verse.first().0
  lines =: conj(lines "I know an old lady who swallowed a $animal.")
  lines =: conj(lines barn.$verse.first().1)
  lines =:
    if 0 < verse < barn.#.--:
      conj _ barn.0:first.1:
        reduce _ lines (verse .. 0):
          fn(lines verse):
            lines =:
              if verse.?:
                then:
                  animal1 animal2 =:
                    L(barn.$verse.first().0 barn.nth(verse.--):first.0)
                  line =: "She swallowed the $animal1 to catch the $animal2."
                  line =:
                    if animal2 == 'spider':
                      replace(line /\./ replace(barn.1:first.1 /It/ ' that'))
                      line
                  conj lines: line
                else: lines
            =>: lines
      =>: lines
  conj lines: ''
