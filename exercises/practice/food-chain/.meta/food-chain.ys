!YS-v0

barn =::
- fly: I don't know why she swallowed the fly. Perhaps she'll die.
- spider: It wriggled and jiggled and tickled inside her.
- bird: How absurd to swallow a bird!
- cat: Imagine that, to swallow a cat!
- dog: What a hog, to swallow a dog!
- goat: Just opened her throat and swallowed a goat!
- cow: I don't know how she swallowed a cow!
- horse: She's dead, of course!

defn recite(start-verse end-verse):
  reduce(add-verse [] (start-verse.-- .. end-verse.--)):drop-last

defn add-verse(lines verse):
  animal =: barn.$verse:first.0
  lines .=: conj("I know an old lady who swallowed a $animal.")
  lines .=: conj(barn.$verse:first.1)
  conj _ '':
    or _ lines:
      when 0 < verse < barn.#.--:
        conj _ barn.0:first.1:
          reduce _ lines (verse .. 1):
            fn(lines verse):
              conj lines:
                animal1 =: barn.$verse:first.0
                animal2 =: barn.nth(verse.--):first.0
                "She swallowed the $animal1 to catch the $animal2.":
                  .call(fn([s] if((animal2 != 'spider') s
                   replace(s /\./ replace(barn.1:first.1 /It/ ' that'))))_)
