!yamlscript/v0

account =: atom(nil)

defn reset-test-state():
  reset! account: nil

defn bank-account(operations):
  last:
    for operation operations:
      operation
        .operation
        .str('do-' _)
        .call(operation)

defn- do-open(op):
  when deref(account):
    die: 'account already open'
  reset! account: 0

defn- do-close(op):
  when-not deref(account):
    die: 'account not open'
  reset! account: nil

defn- do-balance(op):
  balance =: deref(account)
  if-not balance:
    die: 'account not open'
    else: balance

defn- do-deposit(op):
  when-not deref(account):
    die: 'account not open'
  swap! account +: op:get-amount

defn- do-withdraw(op):
  balance =: deref(account)
  when-not balance:
    die: 'account not open'
  amount =: op:get-amount
  when amount > balance:
    die: 'amount must be less than balance'
  swap! account -: amount

defn- get-amount(op):
  amount =: op.amount
  if amount < 0:
    die: 'amount must be greater than 0'
    else: amount
