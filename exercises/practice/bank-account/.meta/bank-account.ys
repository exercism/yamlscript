!YS-v0

defn bank-account(operations):
  reduce action 'closed' operations:

defn- action(bal op):
  closed? =: eq('closed')
  case op.operation:
    'open':
      if closed?(bal): 0, die('account already open')
    'close':
      if closed?(bal): die('account not open'), 'closed'
    'balance':
      if closed?(bal): die('account not open'), bal
    'deposit':
      cond:
        bal:closed?: die('account not open')
        op.amount <= 0: die('amount must be greater than 0')
        else: bal + op.amount
    'withdraw':
      cond:
        bal:closed?: die('account not open')
        op.amount <= 0: die('amount must be greater than 0')
        op.amount > bal: die('amount must be less than balance')
        else: bal - op.amount

reset-test-state =: \()
